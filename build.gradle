buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
    }
}

allprojects {
  repositories {
      google()
      mavenCentral()
  }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.3"
    ndkVersion "25.1.8937393"

    defaultConfig {
        applicationId "org.meumeu.wivrn"
        // for Vulkan, need at least 24
        // for AImage_getHardwareBuffer, API 26
        minSdkVersion 29
        compileSdkVersion 29

        versionName = "1.0"
        versionCode = 1

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_static', '-DCMAKE_EXPORT_COMPILE_COMMANDS=ON', '-DUSE_SYSTEM_FREETYPE=OFF'
                resValue "string", "app_name", "WiVRn"
            }
        }
    }

    signingConfigs {
        release {
            storeFile file("ks.keystore")
            keyAlias "default_key"
            storePassword signingKeyPassword // Set in gradle.properties
            keyPassword signingKeyPassword
            enableV2Signing true
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "device", "validation"

    productFlavors {
        oculus {
            dimension "device"
            externalNativeBuild {
                cmake {
                    abiFilters 'arm64-v8a'
                    arguments '-DOCULUS_SDK=' + file('OculusSDK').absolutePath
                }
            }
        }

        validationlayer {
            dimension "validation"
            externalNativeBuild {
                cmake {
                    abiFilters 'arm64-v8a'
                    arguments '-DUSE_ANDROID_VALIDATION_LAYER=ON'
                }
            }
        }

        novalidationlayer {
            dimension "validation"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDir 'resources'
        }
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path    'CMakeLists.txt'
        }
    }
}
